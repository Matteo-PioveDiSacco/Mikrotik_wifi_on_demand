# Ver.: 3.01 del 20240711
# Ordinato il codice per fare chiarezza nei vari passaggi, nessuna nuova features.
# Occhio che quando si copiano, importano o editano gli script  necessario a non lasciare spazi
# tra le righe di codice



# Ver.: 3.00 del 20240620
#
# Ora viene gestita la doppia pressione del tasto, quando avviene una doppia pressione
# si resetta tutto e il wifi si spegne, questa condizione va bene per ripristinare
# il funzionamento nel caso il sistema si incasini.
# Sono stati aggiunti gli script esterni:
# - pulsante
# - resetta

# Ver.: 2.00 del 20240531
#
# Alla pressione del tasto MODE viene abilitata la WIFI guest per X ore.
# Ogni ulteriore pressione del tasto non modifica il countdown.


# setta le variabili

## Interfaccia che fornisce il Wifi
:local interfaccia "wlan1"

# Durata del Wifi (ore:minuti:secondi)
:local tempo "00:02:00"

# Tempo di durata del segnale di abbattimento della connessione (ore:minuti:secondi)
:local AlertDown "00:01:00"

# Interfaccia che possiede il POE-OUT dove e' connesso il pulsante luminoso
:local IfPoe "ether2"

# Variabile che indica se lo script di attivazione  in funzione o meno
:global RunAll

# Come prima operazione, e necessario impostare le interfacce nella giusta configurazione ma solo dopo
# un reboot, pertanto il codice che segue viene eseguito solamente alla prima pressione del tasto
# dopo l'accensione o il reboot del dispositivo.

:if (([:typeof $RunAll] ~ "(nil|nothing)")and([/interface/wireless/get $interfaccia value-name=disabled]=false)) do={
  /interface/wireless/set $interfaccia disabled=yes
  /interface/ethernet/set $IfPoe poe-out=off
}

# Controlla se l'interfaccia e' gia' attiva
:if ([/interface/wireless/get $interfaccia value-name=disabled]) do={
  /interface/wireless/set $interfaccia disabled=no
  /interface/ethernet/set $IfPoe poe-out=forced-on
  :local TimeNow [/system clock get time]
  :local EndTime ($TimeNow + $tempo)
  :local AlertTime ($EndTime - $AlertDown)
  :log info message=("Pulsante premuto: Attivo Wifi Guest e avvio countdown")
  :set RunAll 1
  :while (([/system clock get time] < $EndTime)and($RunAll=1)) do={
    :if ([/system clock get time] > $AlertTime) do={
      /interface/ethernet/set $IfPoe poe-out=off
      :delay 500ms
      /interface/ethernet/set $IfPoe poe-out=forced-on
      :delay 500ms
    } else={ :delay 1s }

  }

  :log info message=("Countdown terminato, disabilito Wifi Guest")
  /interface/wireless/set $interfaccia disabled=yes
  /interface/ethernet/set $IfPoe poe-out=off
  :set RunAll 0
}
